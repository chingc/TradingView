//@version=5

// This indicator tracks price change over an arbitrary number of trading sessions.
// What is the orange number? The orange number is the percentage (in decimal) of bars
// that are highlighted over a given highlight range. If 3 bars are highlighted over a
// highlight range of 50, then the orange number will be 0.06.
// GitHub: https://github.com/chingc/TradingView

indicator(title='Price Change History', shorttitle='Price Change', precision=2)

// user settings
src = input.source(defval=close, title='Source')
length = input.int(defval=1, title='Trading Sessions', minval=1)  // trading sessions to consider
highlightUpper = input.float(defval=5, title='Highlight Above', minval=0)  // highlight values above this
highlightLower = input.float(defval=-5, title='Highlight Below', maxval=0)  // highlight values below this
highlightRange = input.int(defval=50, title='Highlight Range', minval=1)  // trading sessions to consider for the orange number
asPercent = input.bool(defval=true, title='As Percentage') // show change as percentage or as currency

// the price change dataset
dataset = asPercent ? ta.change(src, length) / src[length] * 100 : ta.change(src, length)

// paint a bar
paint(bar) =>
    switch
        bar > highlightUpper => color.aqua
        bar < highlightLower => color.fuchsia
        bar > 0 => color.green
        => color.red

// draw the bars
plot(series=dataset, style=plot.style_columns, color=paint(dataset))

// calculate the orange number
orangeNumber(bars) =>
    count = 0
    for i = 0 to highlightRange - 1
        if bars[i] > highlightUpper or bars[i] < highlightLower
            count += 1
    count / highlightRange

// this plot shows the orange number with a value between 0 and 1 (inclusive)
// the graph itself isn't helpful and is hidden by setting transp to 100
plot(series=orangeNumber(dataset), color=color.new(color.orange, transp=100))
